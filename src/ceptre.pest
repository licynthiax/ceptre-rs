// util
number = @{ "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
notnewline = { !"\n" }

ident = @{ !"o-" ~ id_start ~ id_cont* }
id_start = { ASCII_ALPHA | "\\" | "/" }
id_cont = { id_start | ASCII_DIGIT | "_" | "'" | "-" }

trace = {
    "#trace" ~ number ~ ident ~ atomic ~ "."
  | "#trace" ~ wildcard ~ ident ~ atomic ~ "."
}

builtin = {
    "#builtin" ~ nat ~ ident ~ "."
  | "#builtin" ~ nat_zero ~ ident ~ "."
  | "#builtin" ~ nat_succ ~ ident ~ "."
}
nat = { "NAT" }
nat_zero = { "NAT_ZERO" }
nat_succ = { "NAT_SUCC" }

interactive = { "#interactive" ~ ident ~ "." }

pred = { "pred" }
stage = { "stage" }
context = { "context" }

// operators
comma = { "," }    // precr 10
colon = { ":" }    // precr 20, nonassoc
larrow = { "<-" }  // precl 30
llolli = { "o-" }  // precl 30
rlolli = { "-o" }  // precr 40
rarrow = { "->" }  // precr 40
star = { "*" }     // precr 50
bang = { "!" }     // precl 60
dollar = { "$" }   // precl 60
unify = { "==" }   // precr 80, nonassoc
differ = { "<>" }  // precr 80, nonassoc
wildcard = { "_" }

// skip/non-ceptre
WHITESPACE = _{ " " | "\n" | "\r" | "\t" }
COMMENT = _{ "%" ~ !("***") ~ (notnewline ~ ANY)* }

// annotations
annote = { "%***" ~ annote_content }
// TODO: actual inner stuff
annote_content = @{ (notnewline ~ ANY)* }

// rules
tops = { top* }
top = {
    annote_syn
  | stage ~ ident ~ "{" ~ top* ~ "}" ~ op_period
  | context ~ ident ~ op_equals ~ "{" ~ op_syn ~ "}" ~ op_period
  | special
}

special = { trace | builtin | interactive }

syn = { prefix* ~ primary ~ (infix ~ prefix* ~ primary)* }
annote_syn = { annote? ~ syn ~ "." }
primary = _{ atomics }
atomics = { atomic+ }
infix = _{ colon | rlolli | llolli | rarrow | larrow | star | comma | unify | differ }
prefix = _{ bang | dollar }

braces = { "{" ~ syn ~ "}" }
parens = { "(" ~ atomics ~ ")" }
empty_braces = { "{}" }
unit = { "()" }

atomic = {
    parens
  | braces
  | empty_braces
  | unit
  | ident
  | wildcard
  | pred
  | number
}

op_equals = _{ "="? }
op_period = _{ "."? }
op_syn = { syn? }

